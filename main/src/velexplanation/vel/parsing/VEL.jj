/**
 * A grammar for objectives based on Andreas & Ruslan's JavaCC parser.
 */
options
{
	STATIC = false;
	LOOKAHEAD = 2;
	FORCE_LA_CHECK = true;
}

PARSER_BEGIN(VELParser)
/* Insert the default contents of the file here.  May need imoprts etc.
*/
package velexplanation.vel.parsing;

import burlap.mdp.core.action.ActionType;
import burlap.mdp.core.state.State;
import burlap.mdp.core.oo.propositional.PropositionalFunction;
import burlap.mdp.core.oo.state.OOState;
import burlap.mdp.core.oo.state.OOStateUtilities;
import burlap.mdp.core.oo.state.ObjectInstance;
import burlap.mdp.singleagent.SADomain;
import burlap.mdp.singleagent.oo.OOSADomain;
import velexplanation.actions.ObjectParameterizedActionType;
import velexplanation.rabin.ParamPlaceholder;
import velexplanation.rewardvector.comparator.*;
import velexplanation.vel.*;
import velexplanation.*;
import rabinizer.formulas.*;
import rabinizer.exec.*;
import rabinizer.bdd.*;
import velexplanation.misc.Pair;

import java.io.StringReader;
import java.util.*;
import java.util.stream.Collectors;
import org.apache.commons.math3.linear.RealVector;

public class VELParser
{
	protected Map<String, List<String>> props;
	protected Map<String, List<String>> objs;
	protected Map<String, String> objectiveParams;
	protected Set<String> specificObjects;
	protected SADomain d = null;

        protected Globals globals;

        public VELParser(Globals globals, java.io.Reader reader) {
            this(reader);
            this.globals = globals;
        }

    public static Pair<List<VELObjective>,Comparator<RealVector>> parseFromText(String text, SADomain d, State s) throws ParseException {
        Globals globals = new Globals();
        globals.bddForFormulae = new BDDForFormulae();
        globals.bddForVariables = new BDDForVariables();
        globals.vsBDD = new ValuationSetBDD(globals);
        globals.aV = new AllValuations();

        return new VELParser(globals,new StringReader(text)).parse(d,s);
    }


	public Pair<List<VELObjective>,Comparator<RealVector>> parse(SADomain d, State s) throws ParseException {
	            this.d = d;
                this.props = new HashMap<String, List<String>>();
                this.specificObjects = new HashSet<String>();

                if(d instanceof OOSADomain) {
                    for(PropositionalFunction pf : ((OOSADomain)d).propFunctions()) {
                         props.put(pf.getName(), Arrays.asList(pf.getParameterClasses()));
                    }
                }

                Map<String, List<ObjectInstance>> map;
                if(s instanceof OOState) {
                    map = OOStateUtilities.objectsByClass((OOState)s);
                } else {
                    map = new HashMap<String, List<ObjectInstance>>();
                }

                 this.objs = new HashMap<String, List<String>>();
                for(String key : map.keySet()) {
                    List<String> objects = new ArrayList<String>();
                    for(ObjectInstance o : map.get(key)) {
                        objects.add(o.name());
                    }
                    this.objs.put(key, objects);
                }

                globals.bddForVariables.bijectionIdAtom = new BijectionIdAtom();
                this.objectiveParams = new HashMap<String, String>();
                return weightedObjectives();
	}
}

PARSER_END(VELParser)

SKIP :
{
  " "
| "\r"
| "\t"
| "\n"
}

TOKEN : /* OPERATORS */
{
  < FOP : "F" >
| < GOP : "G" >
| < XOP : "X" >
| < NEG : "!" >
| < UOP : "U" >
| < VOP : "V" >
| < WOP : "W" >
| < AND : "&" >
| < DUR : "*" >
| < OR : "|" >
| < IMP : "->" >
| < LPAR : "(" >
| < RPAR : ")" >
| < LANG : "<" >
| < RANG : ">" >
| < COM : "," >
| < SEMI : ";" >
| < COL : ":" >
| < UNIV : "A" >
| < EXIS : "E" >
}

TOKEN :
{
  < TRUE : ("true" | "1") >
| < FALSE : ("false" | "0") >
| < ID :
    (
      [ "A"-"Z" ]
    | [ "a"-"z" ]
    )
    (
      [ "0"-"9" ]
    | [ "A"-"Z" ]
    | [ "a"-"z" ]
    )* >
| < NUM :
      ([ "0" - "9"])+
      ( (".") (["0"-"9"])+)?
  >
}

Pair<List<VELObjective>, Comparator<RealVector>> weightedObjectives() :
{
	VELObjective result;

    List<VELObjective> toReturn = new ArrayList<VELObjective>();
	List<Double> weights = new ArrayList<Double>();
	List<Integer> priorities = new ArrayList<Integer>();
	int curPriority = 0;

	Formula r;
        double weight = 1.0;
        Token t;
}
{
        (t = < NUM > { weight = Double.parseDouble(t.image); } < COL >)?
	result = singleObjective()
        {

            toReturn.add(result);
            weights.add(weight);
            priorities.add(curPriority);
            weight = 1.0;
        }
	(
		(< SEMI > | < RANG > < RANG > {
		    curPriority -= 1;
		})
                (t = < NUM > { weight = Double.parseDouble(t.image); } < COL >)?
                result = singleObjective()
		{
                        toReturn.add(result);
                        weights.add(weight);
                        priorities.add(curPriority);
                        weight = 1.0;
		}
	)*
	{
            return new Pair<List<VELObjective>,
            Comparator<RealVector>>(toReturn, new MixedLexicographicWeightedComparator(
                priorities,
                weights,
                1E-3
            ));
	}
}

VELObjective singleObjective() :
{
    Set<String> costlyVariables = new HashSet<String>();
    List<String> quantifiedVariables = new ArrayList<String>();
    List<Boolean> quantificationTypes = new ArrayList<Boolean>();
    boolean durative = false;
	Formula formula;
	objectiveParams.clear();
	String var;
}
{
(
(<LANG> var=<ID>.image {costlyVariables.add(var); }
(<COM> var=<ID>.image {costlyVariables.add(var);})*
<RANG>)?
((<UNIV> var=<ID>.image {
    quantifiedVariables.add(var);
    quantificationTypes.add(true);
})
|(<EXIS> var=<ID>.image {
    quantifiedVariables.add(var);
    quantificationTypes.add(false);
})
)*
(<DUR> {durative = true;})?
<COL>)?
	formula = implication()
        {
            return new VELObjective((OOSADomain)d, formula, costlyVariables, quantifiedVariables,
                               quantificationTypes, durative);
        }
}

Formula implication() :
{
 Formula r = null;
 Formula result;
}
{
  result = disjunction()
  (
   < IMP > r = disjunction()
   { result = new Implication(result, r); }
  )?
  { return result; }
}

Formula disjunction() :
{
  Formula r = null;
  Formula result;
}
{
  result = conjunction()
  (
    < OR > r = conjunction()
    {
      result = new Disjunction(result, r);
    }
  )*
  {
    return result;
  }
}

Formula conjunction() :
{
  Formula result;
  Formula r = null;
}
{
  result = until()
  (
    < AND > r = until()
    {
      result = new Conjunction(result, r);
    }
  )*
  {
    return result;
  }
}

Formula until() :
{
  Formula result;
  Formula r = null;
}
{
  result = unaryOp()
  ((
    < UOP > r = unaryOp()
    {
 	  result = new UOperator(result, r);
    }
  ) | (
      < VOP > r = unaryOp()
      {
      result = new VOperator(result, r);
      }
  ) | (
    < WOP > r = unaryOp()
         {
         result = new VOperator(new Disjunction(new Negation(result), r),
                                new Disjunction(result,r));
         }
  ))?
  {
    return result;
  }
}

Formula unaryOp() :
{
  Formula f;
}
{
  < FOP > f = unaryOp()
  {
    return new FOperator(f);
  }
| < GOP > f = unaryOp()
  {
    return new GOperator(f);
  }
| < XOP > f = unaryOp()
  {
    return new XOperator(f);
  }
| < NEG > f = unaryOp()
  {
    return new Negation(f);
  }  
| f = atom()
  {
    return f;
  }
}

Formula atom() :
{
  String atomString;
  int id;
  Formula f;
  String nextParam;
  List<String> params = new ArrayList<String>();
}
{
  < TRUE >
  {
	return new BooleanConstant(true, globals);
  }
| < FALSE >
  {
	return new BooleanConstant(false, globals);
  }
|  atomString = < ID >.image (< LPAR >
   	      	nextParam = < ID >.image { params.add(nextParam); }
		( < COM > nextParam = < ID >.image { params.add(nextParam); } )*
   	      < RPAR >) ?
  {
	if(!props.containsKey(atomString)) throw new ParseException("Can't read atom " + atomString);
	if(params.size() != props.get(atomString).size()) throw new ParseException();
	for(int i =0; i < params.size(); i++) {
		String param = params.get(i);
		String cls = props.get(atomString).get(i);
		if(objs.get(cls).contains(param)) {
            specificObjects.add(param);
		}
		if(objectiveParams.containsKey(param) && !objectiveParams.get(param).equals(cls)) throw new ParseException();
		else if(!objectiveParams.containsKey(param)) {
		     objectiveParams.put(param, cls);
		}
	}
        if(params.isEmpty()) {
            int i = globals.bddForVariables.bijectionIdAtom.id(atomString);
            return new Literal(atomString, i, false, globals);
        }
        f = new ParamPlaceholder(atomString, params, globals);
        for(String specificObject : specificObjects) {
            f = f.applyParam(specificObject,specificObject);
        }
        return f;
  }
| < LPAR > f = implication() < RPAR >
  {
    return f;
  }
}